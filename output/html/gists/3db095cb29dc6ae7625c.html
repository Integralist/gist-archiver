<!DOCTYPE html>
<html>
<head>
<meta charset="utf-8">
<title>Reading "Programming Concurrency on the JVM" I found an example (which I've modified below) using Clojure to solve a classic concurrency dilemma by using the STM to help keep things sane.</title>
<style>
body { font-family: sans-serif; margin: 20px; line-height: 1.6; background-color: #f4f4f4; color: #333; }
.container { max-width: 800px; margin: auto; background: #fff; padding: 20px; border-radius: 8px; box-shadow: 0 0 10px rgba(0,0,0,0.1); }
pre { background: #f0f0f0; padding: 1em; border-radius: 5px; overflow-x: auto; border: 1px solid #ddd; }
code { font-family: 'SFMono-Regular', Consolas, 'Liberation Mono', Menlo, Courier, monospace; font-size: 0.9em;}
h1, h2 { border-bottom: 1px solid #eee; padding-bottom: 0.3em;}
a.back-link { display: inline-block; margin-bottom: 20px; color: #007bff; text-decoration: none; }
a.back-link:hover { text-decoration: underline; }
</style>
</head>
<body>
<div class="container">
<a href="../index.html" class="back-link">&laquo; Back to Index</a>
<h1 id="reading-programming-concurrency-on-the-jvm-i-found-an-example-which-i-ve-modified-below-using-clojure-to-solve-a-classic-concurrency-dilemma-by-using-the-stm-to-help-keep-things-sane">Reading &ldquo;Programming Concurrency on the JVM&rdquo; I found an example (which I&rsquo;ve modified below) using Clojure to solve a classic concurrency dilemma by using the STM to help keep things sane.</h1>

<h2 id="stm-skew-clj">stm_skew.clj</h2>

<pre><code class="language-clojure">; we need to have 1000 (or more) across both accounts
; if we have less than a 1000 then there is a violation

(def current-account (ref 500))
(def savings-account (ref 600))

; the `alter` function allows us to modify a reference (`@from` in this case
; which points to either the reference `current-account` or `savings-account`)

(defn unsafe-withdraw [from constraint amount]
  (dosync
    (let [total (+ @from @constraint)]
      (Thread/sleep 1000) ; blocks &amp; so allows context switch to other future(thread) to start
      (if (&gt;= (- total amount) 1000)
        (alter from - amount)
        (println &quot;Sorry, can't withdraw due to constraint violation&quot;)))))

(defn safe-withdraw [from constraint amount]
  (dosync
    (let [total (+ @from (ensure constraint))] ; the `ensure` function is the secret sauce!
                                               ; also notice we don't deference the value (@constraint)
      (Thread/sleep 1000)
      (if (&gt;= (- total amount) 1000)
        (alter from - amount)
        (println &quot;Sorry, can't withdraw due to constraint violation&quot;)))))

; `ensure` let's us tell a transaction (remember `dosync` is using the STM)
; to watch a variable that is read and not modified.
; STM will ensure writes are comitted only if the values we've read have not
; changed outside the transaction. It'll then retry the transaction otherwise.

; UNSAFE EXAMPLE EXECUTION

(println &quot;UNSAFE: BEFORE&quot;)
(println &quot;Current Account balance is&quot; @current-account)
(println &quot;Savings Account balance is&quot; @savings-account)
(println &quot;Total balance is&quot; (+ @current-account @savings-account))

(future (unsafe-withdraw current-account savings-account 100))
(future (unsafe-withdraw savings-account current-account 100))

(Thread/sleep 2000) ; allowing enough time for both futures(threads) to complete

(println &quot;UNSAFE: AFTER&quot;)
(println &quot;Current Account balance is&quot; @current-account)
(println &quot;Savings Account balance is&quot; @savings-account)
(println &quot;Total balance is&quot; (+ @current-account @savings-account))

; OUTPUT (notice our code is not thread-safe as we've violated the account requirements)...

; UNSAFE: BEFORE
; Current Account balance is 500
; Savings Account balance is 600
; Total balance is 1100

; UNSAFE: AFTER
; Current Account balance is 400
; Savings Account balance is 500
; Total balance is 900

; ...WE'RE UNDER A 1000 ACROSS BOTH ACCOUNTS :-(


; SAFE EXAMPLE EXECUTION

(dosync (ref-set current-account 500)) ; reset value
(dosync (ref-set savings-account 600)) ; reset value

(println &quot;SAFE: BEFORE&quot;)
(println &quot;Current Account balance is&quot; @current-account)
(println &quot;Savings Account balance is&quot; @savings-account)
(println &quot;Total balance is&quot; (+ @current-account @savings-account))

(future (safe-withdraw current-account savings-account 100))
(future (safe-withdraw savings-account current-account 100))

(Thread/sleep 2000) ; allowing enough time for both futures(threads) to complete

(println &quot;SAFE: AFTER&quot;)
(println &quot;Current Account balance is&quot; @current-account)
(println &quot;Savings Account balance is&quot; @savings-account)
(println &quot;Total balance is&quot; (+ @current-account @savings-account))

; OUTPUT (notice our code is now thread-safe)...

; SAFE: BEFORE
; Current Account balance is 500
; Savings Account balance is 600
; Total balance is 1100

; SAFE: AFTER
; Current Account balance is 400
; Savings Account balance is 600
; Total balance is 1000

; ...ONLY ISSUE IS THAT IT SEEMS LIKE THE STM HASN'T RETRIED THE TRANSACTION?
; BECAUSE THERE IS NO WARNING MESSAGE PRINTED (AS PER THE `ELSE` STATEMENT)
; ALSO! MOST OF THE TIME THE BALANCE STAYS AS 1100 (NOTHING DEDUCTED FROM EITHER ACCOUNT)
; AND ON THE RARE OCCASION THE SAVINGS ACCOUNT WILL BE DEDUCTED FROM RATHER THAN THE CURRENT ACCOUNT
</code></pre>

</div>
</body>
</html>